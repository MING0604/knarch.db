/*
 * Copyright (c) 2018 Touchlab Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'kotlin-platform-native'

sourceSets {
    main {
        component {
            baseName.set("knarch")
            target 'ios_arm32', 'ios_arm64', 'ios_x64', 'macos_x64'
            outputKinds = [KLIBRARY]
            publishSources = false
            publishJavadoc = false
        }
    }
    //nativeLibraries
    test {
        component {
            baseName.set("knarch")
            extraOpts("-nativelibrary", "../../cpp/bcdist/macos_x64/tlruntime.bc")
            extraOpts("-linkerOpts", "-lsqlite3")
        }
    }
}



void mergeCppOutput(String config, String arch){
    ant.zip(
            update: "true",
            destfile: "${project.buildDir}/lib/main/$config/$arch/knarch.klib"
    ) {
        zipfileset(
                dir: "${project.projectDir}/../../cpp/bcdist/$arch",
                prefix: "targets/$arch/native"
        ) {
            include(name: '**.bc')
        }
    }
}

task mergeCppAll(dependsOn:build) {
    doLast {
        mergeCppOutput("debug", "ios_arm32")
        mergeCppOutput("debug", "ios_arm64")
        mergeCppOutput("debug", "ios_x64")
        mergeCppOutput("debug", "macos_x64")
        mergeCppOutput("release", "ios_arm32")
        mergeCppOutput("release", "ios_arm64")
        mergeCppOutput("release", "ios_x64")
        mergeCppOutput("release", "macos_x64")
    }
}

apply plugin: 'maven-publish'

apply from: '../gradle/configpublishnative.gradle'

if(project.hasProperty('deployIos')) {
    apply from: '../gradle/configbintray.gradle'
}


dependencies {
    expectedBy project(':knarch-common')
    implementation "com.jakewharton.timber:timberNative:$timber_version"
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

tasks.findByName("publish").dependsOn("mergeCppAll")